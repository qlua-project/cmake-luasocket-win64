@echo off
if /I NOT "%1" == "/ENV" setlocal
call "C:\Program Files (x86)\Microsoft Visual Studio\2022\BuildTools\VC\Auxiliary\Build\vcvars64.bat"
if /I "%1" == "/ENV" goto EOF

set "ALLSTEPS="
set "CURRENT_DIR=%~dp0"

if /I "%1"=="/DEBUG" (
    set cmake_build_dir=cmake-build-debug-%VSCMD_ARG_HOST_ARCH%
    set CMAKE_BUILD_TYPE=Debug
    set STEP=%2
) else (
    set cmake_build_dir=cmake-build-release-%VSCMD_ARG_HOST_ARCH%
    set CMAKE_BUILD_TYPE=Release
    set STEP=%1
)
rem set CMAKE_INSTALL_PREFIX=%CURRENT_DIR:~0,-1%  # cmake 3.29+

if /I "%STEP%" == "/DUMP" goto DUMPBIN
if /I "%STEP%" == "/DUMPBIN" goto DUMPBIN
if /I "%STEP%" == "/CLEAR" goto CLEAN
if /I "%STEP%" == "/CLEAN" goto CLEAN
if /I "%STEP%" == "/DEL" goto DELETE
if /I "%STEP%" == "/DELETE" goto DELETE

goto CMAKESTEP

pushd %~dp0

:DUMPBIN
call dumpbin64.bat %cmake_build_dir%

goto END

:CLEAN
del /Q %cmake_build_dir%\CMakeCache.txt"

goto END

:DELETE
rmdir /s /q "%cmake_build_dir%"

goto END

:CMAKESTEP
mkdir "%~dp0%cmake_build_dir%"
pushd "%~dp0%cmake_build_dir%"

if /I "%STEP%" == "/SYSINFO" goto SYSINFO
if /I "%STEP%" == "/BUILD" goto BUILD
if /I "%STEP%" == "/INSTALL" goto INSTALL
if /I "%STEP%" == "" set ALLSTEPS=yes

:CONFIG
if NOT "%ALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
:: -G "NMake Makefiles" to skip Visual Studio project files which are generated by default.
:: -Wdev Enable developer warnings that are meant for the author of the CMakeLists.txt files.
:: -L This will effectively display current CMake settings.
:: --log-context message() command outputting context attached to each message
rem cmake -G "NMake Makefiles" -Wdev --warn-uninitialized --log-context -L -B "%cmake_build_dir%" --install-prefix "%INSTALL_DIR:~0,-1%"
cmake -G "NMake Makefiles" -Wdev --warn-uninitialized --log-context -L --install-prefix "%CURRENT_DIR:~0,-1%" ..

if ERRORLEVEL==1 goto END
if NOT "%ALLSTEPS%" == "" goto BUILD
goto END

:BUILD
if NOT "%ALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
cmake --build .

if ERRORLEVEL==1 goto END
if NOT "%ALLSTEPS%" == "" goto INSTALL
goto END

:INSTALL
if NOT "%ALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
cmake --install .

if ERRORLEVEL==1 goto END
rem if NOT "%ALLSTEPS%" == "" goto DUMPBIN
goto END

:SYSINFO
:: --system-information [file] it will dump additional information such as the cache, log files etc.
cmake -G "NMake Makefiles" --system-information "cmake-build-sysinfo.txt"

goto END

:END
popd

:ENDLOCAL
endlocal

:EOF
